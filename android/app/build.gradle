plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
}

// --- Load Keystore Properties ---
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
}
def flutterVersionCode = localProperties.getProperty('flutter.versionCode') ?: '1'
def flutterVersionName = localProperties.getProperty('flutter.versionName') ?: '1.0'

android {
    namespace = "com.asal.asalpay"

    // compileSdk = flutter.compileSdkVersion

    compileSdk = 35

    // ndkVersion = flutter.ndkVersion

    ndkVersion '27.0.12077973'

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17

        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {

        jvmTarget = "17"
    }


    packagingOptions {
        doNotStrip '*/.so'
    }

    defaultConfig {
        applicationId = "com.asal.asalpay"
        minSdkVersion = 23  // Required by firebase-auth; was flutter.minSdkVersion (21)
        targetSdk = 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    // APK size: Per-ABI splits = smaller APKs per device. Run: flutter build apk --split-per-abi
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            universalApk true  // Also build universal fallback for devices not in split list
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists() && keystoreProperties["storeFile"]) {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
    }

    buildTypes {
        release {
            // Use release signing only when key.properties and storeFile exist; otherwise debug (so build succeeds)
            signingConfig = (keystorePropertiesFile.exists() && keystoreProperties["storeFile"])
                    ? signingConfigs.release
                    : signingConfigs.debug

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }

        debug {
            debuggable true
        }
    }
}




flutter {
    source = "../.."
}

dependencies {

    // Google Play Services
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    // implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    
    // KEPT: Chinese OCR for bank transfer. See docs/APK_SIZE_REDUCTION.md
    implementation 'com.google.mlkit:text-recognition-chinese:16.0.0'
    // REMOVED: Japanese, Korean, Devanagari - not used. Restore from docs if needed.
    // implementation 'com.google.mlkit:text-recognition-devanagari:16.0.0'
    // implementation 'com.google.mlkit:text-recognition-japanese:16.0.0'
    // implementation 'com.google.mlkit:text-recognition-korean:16.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.24"

    // KEPT: Required by ML Kit text recognition. tensorflow-lite-gpu is optional (uncomment for faster OCR).
    implementation 'org.tensorflow:tensorflow-lite:2.3.0'
    // implementation 'org.tensorflow:tensorflow-lite-gpu:2.3.0'  // Optional: ~15MB, uncomment for GPU acceleration

    implementation 'androidx.work:work-runtime-ktx:2.8.1'

    implementation 'com.google.android.play:integrity:1.3.0'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.3"

    implementation "com.google.android.gms:play-services-identity:18.0.1"

    implementation "androidx.window:window:1.2.0"


   implementation platform('com.google.firebase:firebase-bom:33.4.0')
   implementation 'com.google.firebase:firebase-messaging'
   // play-services-auth already above (line 107) - duplicate removed for clarity
}
